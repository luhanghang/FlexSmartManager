<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" horizontalScrollPolicy="off" verticalScrollPolicy="off" 
	width="100%" height="100%" xmlns:recording="views.recording.*">
	<mx:Script>
		<![CDATA[
			import mx.core.Application;
			import mx.rpc.events.ResultEvent;
			import events.ScheduleEvent;
			import mx.controls.Alert;
			import utils.Utils;
			
			[Bindable]
			public var spot:XML;
			
//			[Bindable]
//			private var gFilter:GlowFilter = Utils.neonFilter();
		
			[Bindable]
			static public var hours:Array = [0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23];
		
			[Bindable]
			static public var days:Array = ['周一','周二','周三','周四','周五','周六','周日'];
			
			public function addItems(days:Array, startHour:String, startMin:String, endHour:String, endMin:String, last:Number, state:Boolean, color:Object):void {
				scheduleAddRPC.url = Utils.get_url("/record_schedules/create");
				var params:URLVariables = new URLVariables;
				params['days[]'] = days;
				params['RecordSchedule[start_hour]'] = startHour;
				params['RecordSchedule[start_min]'] = startMin;
				params['RecordSchedule[end_hour]'] = endHour;
				if(endHour == '00' || endHour == '0') {
					endMin = '0';
				}
				params['RecordSchedule[end_min]'] = endMin;
				params['RecordSchedule[last_time]'] = last;
				params['RecordSchedule[color]'] = color;
				params['RecordSchedule[spot_id]'] = spot.@id;
				params['RecordSchedule[enabled]'] = state?"1":"0"; 
				scheduleAddRPC.send(params);
				Utils.doFreeze(mx.core.Application.application, true);
			}
			
			public function doAddItems(event:ResultEvent):void {
				Utils.doFreeze(mx.core.Application.application, false);
				add_schedules(XML(event.result));	
			}
			
			private var _removeItem:ScheduleItem;
			public function removeItem(item:ScheduleItem):void {
				Utils.doFreeze(mx.core.Application.application, true);
				_removeItem = item;
				scheduleRemoveRPC.url = Utils.get_url("/record_schedules/destroy/" + item.scheduleId);
				scheduleRemoveRPC.send();
			}
			
			private function doRemoveItem(event:ResultEvent):void {
				Utils.doFreeze(mx.core.Application.application, false);
				_removeItem.parent.removeChild(_removeItem);
			}
			
			private function showEditor(event:MouseEvent):void {
				var s:ScheduleItem = event.currentTarget as ScheduleItem;
				scheduleEditor.setScheduleItem(s);
				scheduleEditor.visible = true;
				scheduleEditor.includeInLayout = true;
				scheduleEditor.x = event.localX + s.x;
				if(scheduleEditor.x + scheduleEditor.width > width) {
					scheduleEditor.x = width - scheduleEditor.width;
				}
				scheduleEditor.y = event.localY + s.parent.parent.y;
				if(scheduleEditor.y + scheduleEditor.height > height) {
					scheduleEditor.y = height - scheduleEditor.height;
				}
				s.edit();
				fade.play();		
			}
			
			private function changeSchedule(event:ScheduleEvent):void {
				var item:ScheduleItem = event.item;
				scheduleDay[item.day].setScheduleItem(item);
				scheduleUpdateRPC.url = Utils.get_url("/record_schedules/update/" + item.scheduleId);
				var params:URLVariables = new URLVariables;
				params['RecordSchedule[start_hour]'] = item.startHour;
				params['RecordSchedule[start_min]'] = item.startMin;
				params['RecordSchedule[end_hour]'] = item.endHour;
				if(item.endHour == '00' || item.endHour == '0') {
					item.endMin = '0';
				}
				params['RecordSchedule[end_min]'] = item.endMin;
				params['RecordSchedule[last_time]'] = item.lastTime;
				params['RecordSchedule[enabled]'] = item.state?"1":"0";
				scheduleUpdateRPC.send(params);	
			}
			
			public function removeAllItems():void {
				Utils.doFreeze(mx.core.Application.application, true);
				scheduleRemoveAllRPC.url = Utils.get_url("/record_schedules/destroy_all/" + spot.@id);
				scheduleRemoveAllRPC.send();
			}
			
			private function removeAll():void {
				Utils.doFreeze(mx.core.Application.application, false);
				for (var i:Number = 0; i < days.length; i++) {
					scheduleDay[i].removeAll();
				}	
			}
			
			public function init():void {
				if(spot) {
					scheduleRPC.url = Utils.get_url("/record_schedules/list/" + spot.@id);
					scheduleRPC.send();
				}
			}
			
			public function init_schedules(event:ResultEvent):void {
				removeAll();
				add_schedules(XML(event.result));
			}
			
			private function add_schedules(items:XML):void {
				for each(var item:XML in items.Schedule) {
					var c:ScheduleItem = scheduleDay[int(item.weekDay)].addItem(item.startHour, item.startMin, item.endHour, 
					item.endMin, item.lastTime, item.enabled == "1"?true:false, int(item.color));
					c.scheduleId = item.@id;
					c.addEventListener(MouseEvent.DOUBLE_CLICK, showEditor);	
				}
			}
		]]>
	</mx:Script>
	<mx:HTTPService id="scheduleAddRPC" method="POST" useProxy="false" showBusyCursor="true" resultFormat="xml" result="doAddItems(event)"/>
	<mx:HTTPService id="scheduleRPC" useProxy="false" showBusyCursor="true" resultFormat="xml" result="init_schedules(event)"/>
	<mx:HTTPService id="scheduleUpdateRPC" method="POST" useProxy="false" showBusyCursor="true"/>
	<mx:HTTPService id="scheduleRemoveRPC" useProxy="false" showBusyCursor="true" result="doRemoveItem(event)"/>
	<mx:HTTPService id="scheduleRemoveAllRPC" useProxy="false" showBusyCursor="true" result="removeAll()"/>
	
	<mx:VBox y="0" width="100%" height="100%" left="-1" right="-1" verticalGap="-1">
		<mx:Repeater id="rptD" dataProvider="{days}">
			<recording:ScheduleDay id="scheduleDay" height="14%" day="{rptD.currentIndex}"/>	
		</mx:Repeater>		
	</mx:VBox>
	
	<mx:Fade id="fade" alphaFrom="0" alphaTo="1" target="{scheduleEditor}"/>
	<recording:ScheduleEditor id="scheduleEditor" ScheduleChange="changeSchedule(event)"/>
</mx:Canvas>
